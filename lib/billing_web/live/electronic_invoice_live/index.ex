defmodule BillingWeb.ElectronicInvoiceLive.Index do
  use BillingWeb, :live_view

  alias Billing.ElectronicInvoices
  alias Billing.Invoices.ElectronicInvoice

  @impl true
  def render(assigns) do
    ~H"""
    <Layouts.app flash={@flash} current_scope={@current_scope}>
      <.header>
        Listing Electronic Invoices
      </.header>

      <.table
        id="electronic_invoices"
        rows={@streams.electronic_invoices}
        row_click={
          fn {_id, electronic_invoice} ->
            JS.navigate(~p"/electronic_invoices/#{electronic_invoice}")
          end
        }
      >
        <:col :let={{_id, electronic_invoice}} label="Invoice">
          <.link navigate={~p"/invoices/#{electronic_invoice.invoice_id}"} class="link">
            {electronic_invoice.invoice_id}
          </.link>
        </:col>
        <:col :let={{_id, electronic_invoice}} label="Name">{electronic_invoice.access_key}</:col>
        <:col :let={{_id, electronic_invoice}} label="State">
          <.electronic_state electronic_invoice={electronic_invoice} />
        </:col>
      </.table>
    </Layouts.app>
    """
  end

  @impl true
  def mount(_params, _session, socket) do
    {:ok,
     socket
     |> assign(:page_title, "Listing Electronic Invoices")
     |> stream(:electronic_invoices, list_electronic_invoices())}
  end

  defp list_electronic_invoices() do
    ElectronicInvoices.list_electronic_invoices()
  end

  defp electronic_state(assigns) do
    assigns =
      assign_new(assigns, :state, fn ->
        if assigns.electronic_invoice do
          %{
            label: ElectronicInvoice.label_status(assigns.electronic_invoice.state),
            css_class: "badge-primary"
          }
        else
          %{label: "Not invoice yet", css_class: "badge-info"}
        end
      end)

    ~H"""
    <span class={["badge", @state.css_class]}>
      {@state.label}
    </span>
    """
  end
end
